plugins {
    id 'java'
    id 'application'
    id 'com.gradleup.shadow' version '8.3.6'
    id 'org.flywaydb.flyway' version '11.8.2'
}

group = 'io.github.coenraadhuman'
version = '0.9.4'

repositories {
    mavenCentral()
}

dependencies {
    // Database
    implementation 'org.jdbi:jdbi3-core:3.49.4'
    implementation 'org.xerial:sqlite-jdbc:3.49.1.0'
    implementation 'org.flywaydb:flyway-core:11.8.2'

    // Logging
    implementation 'org.tinylog:slf4j-tinylog:2.6.2'
    implementation 'org.tinylog:tinylog-impl:2.6.2'

    testImplementation(platform('org.junit:junit-bom:5.10.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

application {
    mainClass = 'io.github.coenraadhuman.directory.bot.Application'
}

distZip {
    dependsOn shadowJar
    into('lib') {
        from(shadowJar.archiveFile)
    }
}

distTar {
    dependsOn shadowJar
    into('lib') {
        from(shadowJar.archiveFile)
    }
}

startScripts {
    dependsOn shadowJar
}

shadowJar {
    archiveBaseName.set('directory-bot')
    archiveClassifier.set('')
    archiveVersion.set(version)
}

tasks.named('jar') {
    enabled = false
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.test {
    useJUnitPlatform()
}